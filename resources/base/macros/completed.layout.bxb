layout-macro-def(completed) {
  params {
    param (quiz) {
      type (Quiz)
      min (Required)
      max (One)
    }
  }

  content {
    section {
      content {
        image{
          url(/images/quizit-logo-white-min.png)
          aspect-ratio(21:9)
          object-fit (Contain)
        }
        for-each (quiz.questions){
          as (question){
            divider
            paragraph {
              value {
                template ("#{value(question.question)}")
              }
              style (Title_XS)
            }
            for-each (question.answers){
              as (answer){
                if(answer.correct == true){
                  cell-card {
                    slot2 {
                      content {
                        order (PrimarySecondary)
                        primary ("#{value(answer.letter)}. #{value(answer.text)}")
                      }
                    }
                  }
                } else {
                  cell-card {
                    slot2 {
                      content {
                        order (PrimarySecondary)
                        secondary ("#{value(answer.letter)}. #{value(answer.text)}")
                      }
                    }
                  }
                }
              }
            }
            if(question.userAnswer != null && question.correct){
              paragraph{
                value("Correct! You chose #{value(question.userAnswer)}")
                style(Detail_L_Soft)
              }
            }
            if(question.userAnswer != null && !question.correct){
              paragraph{
                value("[Incorrect. Your answer: #{value(question.userAnswer)}]")
                style(Detail_L_Soft)
              }
            }
          }
        }
        divider
        paragraph{
          value("Thank you for playing QuizIt! QuizIt is free and was made by just some guy who loves trivia. If you enjoyed this capsule, please rate it ðŸŒŸðŸŒŸðŸŒŸðŸŒŸðŸŒŸ\n\nThe code for QuizIt is open source at my github github.com/jonmpan/bixby-quizit.\n\nSincerely,\nJon P.")
          style(Detail_L_Soft)
        }
      }
    }
  }
}

